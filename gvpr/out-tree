// vim: set sw=2 syntax=c commentstring=//\ %s:

BEGIN {
	int seen[string];
	string target = ARGV[0];
	graph_t g = graph("result", "S");
}

BEG_G {
	setDflt(g, "N", "style", "filled");
	setDflt(g, "N", "shape", "box");
	setDflt(g, "N", "color", "black");
	setDflt(g, "N", "border", "1");
	setDflt(g, "NE", "fontname", "Helvetica");
	g.rankdir = "TD";
	g.splines = "ortho";

	$tvroot = node($, target);
	$tvtype = TV_prepostrev;
	seen[$tvroot.name] = 1;
	$tvroot.level = 1;
	$tvroot.penwidth = 10;
	$tvroot.color = "red";
}

E[head.level] {
	tail.level = head.level + 1;
	tail.fontsize = log(indegreeOf($G, tail) + 1) * 10 + 10;
	tail.fillcolor = colorx(sprintf("%f %f %f %f", 0.5 + (tail.level / 256.0 * 3), 0.8, 1, 0.5), "RGBA");
}

E {
	if (!seen[$.head.name]) {
		$tvroot=NULL;
		return;
	}

	seen[$.tail.name]++;
	$.weight = seen[$.tail.name];
	$.label = seen[$.tail.name];
	$.penwidth = log(seen[$.tail.name] + 1);

	copy(g, $);
	copy(g, $.tail);
	copy(g, $.head);

	return;
}

END_G {
	$O = g;
}
